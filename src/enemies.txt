Actor WolfMonster
{
	//$Category Monsters
	DamageFactor "Enemy", 0
	Radius 16
	Height 64
	mass 100000
	MONSTER
}

Actor WolfBoss : WolfMonster
{
	+FULLVOLDEATH
	+BOSS
	+BOSSDEATH
	+LOOKALLAROUND
}

Actor EnemyBulletPuff : BulletPuff
{
	DamageType "Enemy"
	+NOEXTREMEDEATH
}

actor Guard : WolfMonster 10049 // 3004
{
	Tag "_ Guard"
	Health 25
	PainChance 255
	speed 3
	seesound "guard/sight"
	deathsound "guard/death"
	//DropItem "WolfClip"
	Obituary "%o was killed by a guard."
	States
	{
		Spawn:
			GARD E 1 A_Look
			loop
		Patrol:
			GARD AAAAAABBBBBBCCCCCCDDDDDD 1 A_Look
			loop
		See:
			GARD AAAAAABBBBBBCCCCCCDDDDDD 1 A_Chase
			loop
		Missile:
			GARD GH 5 A_FaceTarget
			GARD I 0 A_PlaySound("enemy/pistol")
			GARD I 5 A_CustomBulletAttack(15,0,1,random(1,5)*3,"EnemyBulletPuff",0,CBAF_NORANDOM)
			GARD H 5
			Goto See
		Pain:
			GARD F 4
			Goto See
		Death:
			GARD J 0 A_SpawnItemEx("WolfClip")
			GARD J 0 A_GiveToTarget("Wolf_Score", 100)
			GARD J 5 A_Scream
			GARD K 5 A_NoBlocking
			GARD LM 5
			GARD N -1
			Stop
	}
}

actor Dog : WolfMonster 10050 // 3002
{
	Tag "_ Dog"
	Health 1
	PainChance 255
	speed 5
	seesound "dog/sight"
	AttackSound "dog/melee"
	deathsound "dog/death"
	Obituary "%o was killed by a dog."
	States
	{
		Spawn:
			SHEP A 1 A_Look
			loop
		See:
			SHEP AAAAAABBBBBBCCCCCCDDDDDD 1 A_Chase
			loop
		Melee:
			SHEP EF 4 A_FaceTarget
			SHEP G 4 A_CustomMeleeAttack(random(1,8) * 3, "dog/active","dog/active","Enemy")
			SHEP FE 4
			Goto See
		Pain:
			Goto See
		Death:
			SHEP H 0 A_GiveToTarget("Wolf_Score", 200)
			SHEP H 5 A_Scream
			SHEP I 5 A_NoBlocking
			SHEP J 5
			SHEP K -1
			Stop
	}
}

actor SSGuard : WolfMonster 10051 // 9
{
	Tag "_ SS Guard"
	Health 100
	PainChance 255
	speed 3
	seesound "ss/sight"
	deathsound "ss/death"
	attacksound "ss/attack"
	//DropItem "WolfMachinegun"
	Obituary "%o was killed by a SS Soldier."
	States
	{
		Spawn:
			SSNZ I 1 Offset (0,-64) A_Look
			loop
		See:
			SSNZ AAAAAABBBBBBCCCCCCDDDDDD 1 A_Chase
			loop
		Missile:
			SSNZ E 5 A_FaceTarget
			SSNZ F 5 A_FaceTarget
			SSNZ G 0 A_PlaySound("ss/attack")
			SSNZ G 5 A_CustomBulletAttack(10,0,1,random(2,6)*3,"EnemyBulletPuff",0,CBAF_NORANDOM)
			SSNZ F 0 A_CPosRefire
			Goto Missile+1
		Pain:
			SSNZ H 4
			Goto See
		Death:
			SSNZ J 0 A_GiveToTarget("Wolf_Score", 500)
			SSNZ J 0 A_JumpIfInTargetInventory("WolfMachinegun", 1, "DropClip")
			SSNZ J 0 A_Jump(256, "DropMachineGun")
		DropClip:
			SSNZ J 0 A_SpawnItemEx("WolfClip")
			Goto FinishDeath
		DropMachineGun:
			SSNZ J 0 A_SpawnItemEx("WolfMachinegun")
		FinishDeath:
			SSNZ J 5 A_Scream
			SSNZ K 5 A_NoBlocking
			SSNZ LM 5
			SSNZ N -1
			Stop
	}
}

actor Mutant : WolfMonster 69
{
	Health 65
	PainChance 255
	speed 3
	//DropItem "WolfClip"
	Obituary "%o was killed by a Mutant."
	deathsound "mutant/death"
	BloodColor "88 00 88"
	States
	{
		Spawn:
			MUTT E 1 A_Look
			loop
		See:
			MUTT AAAAAABBBBBBCCCCCCDDDDDD 1 A_Chase
			loop
		Missile:
			MUTT F 7 A_FaceTarget
			MUTT G 5 A_PosAttack
			MUTT F 7 A_FaceTarget
			MUTT H 0 A_PlaySound("enemy/pistol")
			MUTT H 5 A_CustomBulletAttack(10,0,1,random(1,5)*3,"EnemyBulletPuff",0,CBAF_NORANDOM)
			MUTT H 0 A_CPosRefire
			Goto Missile
		Pain:
			MUTT I 4
			Goto See
		Death:
			MUTT J 0 A_GiveToTarget("Wolf_Score", 700)
			MUTT J 0 A_SpawnItemEx("WolfClip")
			MUTT J 5 A_Scream
			MUTT K 5 A_NoBlocking
			MUTT LM 5
			MUTT N -1
			Stop
	}
}

actor Officer : WolfMonster 3003
{
	Health 50
	PainChance 255
	speed 6
	//DropItem "WolfClip"
	seesound "officer/sight"
	deathsound "officer/death"
	attacksound "enemy/pistol"
	Obituary "%o was killed by an Officer."
	States
	{
		Spawn:
			OFFI E 1 A_Look
			loop
		See:
			OFFI AAAAAABBBBBBCCCCCCDDDDDD 1 A_Chase
			loop
		Missile:
			OFFI FG 4 A_FaceTarget
			OFFI H 0 A_PlaySound("enemy/pistol")
			OFFI H 5 A_CustomBulletAttack(6,0,1,random(2,8)*3,"EnemyBulletPuff",0,CBAF_NORANDOM)
			OFFI G 5  
			Goto See
		Pain:
			OFFI I 4
			Goto See
		Death:
			OFFI J 0 A_GiveToTarget("Wolf_Score", 400)
			OFFI J 0 A_SpawnItemEx("WolfClip")
			OFFI J 5 A_Scream
			OFFI K 5 A_NoBlocking
			OFFI LMN 5
			OFFI O -1
			Stop
	}
}

Actor Wolf_Ghost
{
	mass 100000
	speed 3
	attacksound ""
	+LOOKALLAROUND
}

actor RedGhost : Wolf_Ghost
{
	Obituary "%o was killed by a Red Ghost."
	States
	{
		Spawn:
			RGST AB 5 A_Look
			loop
		See:
			RGST A 0 A_CustomMissile ("GhostMissile")
			RGST A 1 A_Chase
			RGST A 0 A_CustomMissile ("GhostMissile")
			RGST A 1 A_Chase
			RGST A 0 A_CustomMissile ("GhostMissile")
			RGST A 1 A_Chase
			RGST A 0 A_CustomMissile ("GhostMissile")
			RGST A 1 A_Chase
			RGST A 0 A_CustomMissile ("GhostMissile")
			RGST A 1 A_Chase
			RGST B 0 A_CustomMissile ("GhostMissile")
			RGST B 1 A_Chase
			RGST B 0 A_CustomMissile ("GhostMissile")
			RGST B 1 A_Chase
			RGST B 0 A_CustomMissile ("GhostMissile")
			RGST B 1 A_Chase
			RGST B 0 A_CustomMissile ("GhostMissile")
			RGST B 1 A_Chase
			RGST B 0 A_CustomMissile ("GhostMissile")
			RGST B 1 A_Chase
			loop
	}
}

actor PinkGhost : Wolf_Ghost
{
	Obituary "%o was killed by a Pink Ghost."
	States
	{
		Spawn:
			PGST AB 5 A_Look
			loop
		See:
			PGST A 0 A_CustomMissile ("GhostMissile")
			PGST A 1 A_Chase
			PGST A 0 A_CustomMissile ("GhostMissile")
			PGST A 1 A_Chase
			PGST A 0 A_CustomMissile ("GhostMissile")
			PGST A 1 A_Chase
			PGST A 0 A_CustomMissile ("GhostMissile")
			PGST A 1 A_Chase
			PGST A 0 A_CustomMissile ("GhostMissile")
			PGST A 1 A_Chase
			PGST B 0 A_CustomMissile ("GhostMissile")
			PGST B 1 A_Chase
			PGST B 0 A_CustomMissile ("GhostMissile")
			PGST B 1 A_Chase
			PGST B 0 A_CustomMissile ("GhostMissile")
			PGST B 1 A_Chase
			PGST B 0 A_CustomMissile ("GhostMissile")
			PGST B 1 A_Chase
			PGST B 0 A_CustomMissile ("GhostMissile")
			PGST B 1 A_Chase
			loop
	}
}

actor OrangeGhost : Wolf_Ghost
{
	Obituary "%o was killed by a Orange Ghost."
	States
	{
	Spawn:
		OGST AB 5 A_Look
		loop
	See:
		OGST A 0 A_CustomMissile ("GhostMissile")
		OGST A 1 A_Chase
		OGST A 0 A_CustomMissile ("GhostMissile")
		OGST A 1 A_Chase
		OGST A 0 A_CustomMissile ("GhostMissile")
		OGST A 1 A_Chase
		OGST A 0 A_CustomMissile ("GhostMissile")
		OGST A 1 A_Chase
		OGST A 0 A_CustomMissile ("GhostMissile")
		OGST A 1 A_Chase
		OGST B 0 A_CustomMissile ("GhostMissile")
		OGST B 1 A_Chase
		OGST B 0 A_CustomMissile ("GhostMissile")
		OGST B 1 A_Chase
		OGST B 0 A_CustomMissile ("GhostMissile")
		OGST B 1 A_Chase
		OGST B 0 A_CustomMissile ("GhostMissile")
		OGST B 1 A_Chase
		OGST B 0 A_CustomMissile ("GhostMissile")
		OGST B 1 A_Chase
		loop
	}
}

actor BlueGhost : Wolf_Ghost
{
	Obituary "%o was killed by a Blue Ghost."
	States
	{
	Spawn:
		BGST AB 5 A_Look
		loop
	See:
		BGST A 0 A_CustomMissile ("GhostMissile")
		BGST A 1 A_Chase
		BGST A 0 A_CustomMissile ("GhostMissile")
		BGST A 1 A_Chase
		BGST A 0 A_CustomMissile ("GhostMissile")
		BGST A 1 A_Chase
		BGST A 0 A_CustomMissile ("GhostMissile")
		BGST A 1 A_Chase
		BGST A 0 A_CustomMissile ("GhostMissile")
		BGST A 1 A_Chase
		BGST B 0 A_CustomMissile ("GhostMissile")
		BGST B 1 A_Chase
		BGST B 0 A_CustomMissile ("GhostMissile")
		BGST B 1 A_Chase
		BGST B 0 A_CustomMissile ("GhostMissile")
		BGST B 1 A_Chase
		BGST B 0 A_CustomMissile ("GhostMissile")
		BGST B 1 A_Chase
		BGST B 0 A_CustomMissile ("GhostMissile")
		BGST B 1 A_Chase
		loop
	}
}

actor GhostMissile
{
	Radius 16
	Height 16
	Speed 15
	Damage 1
	DamageType "Enemy"
	Projectile
	States
	{
	Spawn:
		BLAH K 1
		Goto Death
	Death:
		BLAH K 0
		stop
	}
}

actor HansGrosse : WolfBoss 65
{
	Health 1200
	PainChance 0
	Speed 3
	seesound "hans/sight"
	deathsound "hans/death"
	attacksound "hans/attack"
	//DropItem "Key_Gold"
	Obituary "%o was killed by Hans Grosse."
	States
	{
		Spawn:
			HANS A 1 A_Look
			loop
		See:
			HANS AAAAABBBBBCCCCCDDDDD 1 A_Chase
			loop
		Missile:
			HANS E 7 A_FaceTarget
			HANS F 0 A_PlaySound("hans/attack")
			HANS F 5 A_CustomBulletAttack(6,0,1,random(3,6)*3,"EnemyBulletPuff",0,CBAF_NORANDOM)
			HANS F 0 A_FaceTarget
			HANS G 0 A_PlaySound("hans/attack")
			HANS G 5 A_CustomBulletAttack(6,0,1,random(3,6)*3,"EnemyBulletPuff",0,CBAF_NORANDOM)
			HANS G 0 A_FaceTarget
			HANS G 0 A_CPosRefire
			Goto Missile+1
		Death:
			HANS H 0 A_GiveToTarget("Wolf_Score", 5000)
			HANS H 0 A_SpawnItemEx("Key_Gold")
			HANS H 5 A_Scream
			HANS I 5 A_NoBlocking
			HANS J 5
			HANS K -1
			Stop
	}
}

actor DrSchabbs : WolfBoss 3001
{
	Health 2400
	PainChance 0
	Speed 3
	seesound "dr/sight"
	deathsound "dr/death"
	Obituary "%o was killed by Dr. Schabbs."
	States
	{
		Spawn:
			SCHA A 1 A_Look
			loop
		See:
			SCHA AAAAABBBBBCCCCCDDDDD 1 A_Chase
			loop
		Missile:
			SCHA E 7 A_FaceTarget
			SCHA F 7 A_CustomMissile ("DrMissile")
			Goto See
		Death:
			SCHA A 0 A_GiveToTarget("Wolf_Score", 5000)
			SCHA A 75 A_Scream
			SCHA G 4 A_NoBlocking
			SCHA HI 4
			SCHA J -1 A_BossDeath
			Stop
	}
}

actor DrMissile
{
	Radius 16
	Height 16
	Speed 15
	Damage 5
	DamageType "Enemy"
	+EXTREMEDEATH
	Projectile
	SeeSound "dr/attack"
	States
	{
		Spawn:
			SCHA KLMN 4
			stop
	}
}

actor RobedHitler : WolfBoss 67
{
	Health 500
	Speed 5
	PainChance 0
	seesound "robe/sight"
	deathsound "robe/death"
	Obituary "%o was killed by a Fake Hitler."
	States
	{
		Spawn:
			ROBE AAAABBBBCCCCBBBB 1 A_Look
			loop
		See:
			ROBE AAAABBBBCCCCBBBB 1 A_Chase
			loop
		Missile:
			ROBE D 0 A_FaceTarget
			ROBE D 5 A_CustomMissile ("RobeMissile")
			ROBE D 0 A_FaceTarget
			ROBE D 5 A_CustomMissile ("RobeMissile")
			ROBE D 0 A_FaceTarget
			ROBE D 5 A_CustomMissile ("RobeMissile")
			ROBE D 0 A_FaceTarget
			ROBE D 5 A_CustomMissile ("RobeMissile")
			ROBE D 0 A_FaceTarget
			ROBE D 5 A_CustomMissile ("RobeMissile")
			ROBE D 0 A_FaceTarget
			ROBE D 5 A_CustomMissile ("RobeMissile")
			ROBE D 0 A_FaceTarget
			ROBE D 5 A_CustomMissile ("RobeMissile")
			ROBE D 0 A_FaceTarget
			ROBE D 5 A_CustomMissile ("RobeMissile")
			ROBE D 0 A_FaceTarget
			ROBE D 5 A_CustomMissile ("RobeMissile")
			Goto See
		Death:
			ROBE E 0 A_GiveToTarget("Wolf_Score", 2000)
			ROBE E 4 A_Scream
			ROBE F 4 A_NoBlocking
			ROBE GHI 4
			ROBE J -1
			Stop
	}
}

actor RobeMissile
{
	Radius 8
	Height 16
	Speed 5
	Damage 5
	DamageType "Enemy"
	+NOEXTREMEDEATH
	Projectile
	SeeSound "robe/attack"
	States
	{
		Spawn:
			ROBE KL 4
			loop
		Death:
			ROBE K 1
			stop
	}
}

actor HitlerSuit : WolfBoss
{
	Health 1200
	PainChance 0
	Speed 2
	+NOBLOOD
	seesound "hitler/sight"
	attacksound "hans/attack"
	deathsound "hitler/death"
	Obituary "%o was killed by Mecha-Hitler."
	States
	{
		Spawn:
			HITL A 1 A_Look
			loop
		See:
			HITL AAAA 1 A_Chase
			HITL B 8 A_Metal
			HITL BBBBCCCC 1 A_Chase
			HITL D 8 A_Metal
			HITL DDDD 1 A_Chase
			loop
		Missile:
			HITL E 5 A_FaceTarget
			HITL F 0 A_PlaySound("hans/attack")
			HITL F 5 A_CustomBulletAttack(6,0,1,random(3,6)*3,"EnemyBulletPuff",0,CBAF_NORANDOM)
			HITL F 0 A_FaceTarget
			HITL G 0 A_PlaySound("hans/attack")
			HITL G 5 A_CustomBulletAttack(6,0,1,random(3,6)*3,"EnemyBulletPuff",0,CBAF_NORANDOM)
			HITL G 0 A_FaceTarget
			HITL G 0 A_CPosRefire
			Goto Missile+1
		Death:
			HITL H 0 A_GiveToTarget("Wolf_Score", 5000)
			HITL H 4 A_Scream
			HITL IJ 4
			HITL K 0 A_NoBlocking
			HITL K 4 A_SpawnItemEx("Hitler")
			HITL K -1
			Stop
	}
}

actor Hitler : WolfBoss
{
	Health 900
	PainChance 0
	Speed 4
	attacksound "hans/attack"
	deathsound "hitler2/death"
	Obituary "%o was killed by Hitler."
	States
	{
		Spawn:
			HITL L 1 A_Look
			loop
		See:
			HITL LLLLMMMMNNNNOOOO 1 A_Chase
			loop
		Missile:
			HITL P 5 A_FaceTarget
			HITL Q 0 A_PlaySound("hans/attack")
			HITL Q 5 A_CustomBulletAttack(6,0,1,random(3,6)*3,"EnemyBulletPuff",0,CBAF_NORANDOM)
			HITL Q 0 A_FaceTarget
			HITL R 0 A_PlaySound("hans/attack")
			HITL R 5 A_CustomBulletAttack(6,0,1,random(3,6)*3,"EnemyBulletPuff",0,CBAF_NORANDOM)
			HITL R 0 A_FaceTarget
			HITL R 0 A_CPosRefire
			Goto Missile+1
		Death:
			HITL L 0 A_GiveToTarget("Wolf_Score", 5000)
			HITL L 75 A_Scream
			HITL S 4 A_NoBlocking
			HITL TUVWXY 4
			HITL Z -1 A_BossDeath
			Stop
	}
}

actor Otto : WolfBoss
{
	Health 1200
	PainChance 0
	Speed 3
	seesound "otto/sight"
	deathsound "otto/death"
	Obituary "%o was killed by Otto."
	States
	{
		Spawn:
			OTTO A 1 A_Look
			loop
		See:
			OTTO AAAAABBBBBCCCCCDDDDD 1 A_Chase
			loop
		Missile:
			OTTO E 7 A_FaceTarget
			OTTO F 7 A_CustomMissile ("EnemyRocket")
			OTTO E 7
			Goto See
		Death:
			OTTO A 0 A_GiveToTarget("Wolf_Score", 5000)
			OTTO A 60 A_Scream
			OTTO G 4 A_NoBlocking
			OTTO HI 4
			OTTO J -1 A_BossDeath
			Stop
	}
}

actor EnemyRocket
{
	Radius 8
	Height 16
	Speed 15
	Damage 20
	DamageType "Enemy"
	Projectile
	SeeSound "weapons/rocklf"
	DeathSound "weapons/rocklx"
	States
	{
		Spawn:
			ROCK A 4 A_SpawnItemEx("EnemyRocketSmoke")
			loop
		Death:
			ROCK FGH 4
			stop
	}
}

actor EnemyRocketSmoke
{
	+NOGRAVITY
	States
	{	
		Spawn:
			ROCK BCDE 4
			Stop
	}
}

actor GretlGrosse : WolfBoss
{
	Health 1200
	PainChance 0
	Speed 3
	seesound "gretl/sight"
	deathsound "gretl/death"
	attacksound "enemy/pistol"
	//DropItem "Key_Gold"
	Obituary "%o was killed by Gretl Grosse."
	States
	{
		Spawn:
			GRTL A 1 A_Look
			loop
		See:
			GRTL AAAAABBBBBCCCCCDDDDD 1 A_Chase
			loop
		Missile:
			GRTL E 7 A_FaceTarget
			GRTL F 0 A_PlaySound("enemy/pistol")
			GRTL F 5 A_CustomBulletAttack(6,0,1,random(3,6)*3,"EnemyBulletPuff",0,CBAF_NORANDOM)
			GRTL F 0 A_FaceTarget
			GRTL G 0 A_PlaySound("enemy/pistol")
			GRTL G 5 A_CustomBulletAttack(6,0,1,random(3,6)*3,"EnemyBulletPuff",0,CBAF_NORANDOM)
			GRTL G 0 A_FaceTarget
			GRTL G 0 A_CPosRefire
			Goto Missile+1
		Death:
			GRTL H 0 A_GiveToTarget("Wolf_Score", 5000)
			GRTL H 0 A_SpawnItemEx("Key_Gold")
			GRTL H 5 A_Scream
			GRTL I 5 A_NoBlocking
			GRTL J 5
			GRTL K -1
			Stop
	}
}

actor GeneralFettgesicht : WolfBoss
{
	Health 1200
	PainChance 0
	Speed 3
	seesound "fett/sight"
	attacksound "hans/attack"
	deathsound "fett/death"
	Obituary "%o was killed by General Fettgesicht."
	States
	{
		Spawn:
			FETT A 1 A_Look
			loop
		See:
			FETT AAAAABBBBBCCCCCDDDDD 1 A_Chase
			loop
		Missile:
			FETT E 7 A_FaceTarget
			FETT F 7 A_CustomMissile ("EnemyRocket")
			FETT F 0 A_FaceTarget
			FETT G 0 A_PlaySound("hans/attack")
			FETT G 5 A_CustomBulletAttack(6,0,1,random(3,6)*3,"EnemyBulletPuff",0,CBAF_NORANDOM)
			FETT G 0 A_FaceTarget
			FETT G 0 A_PlaySound("hans/attack")
			FETT G 5 A_CustomBulletAttack(6,0,1,random(3,6)*3,"EnemyBulletPuff",0,CBAF_NORANDOM)
			FETT H 0 A_FaceTarget
			FETT G 0 A_PlaySound("hans/attack")
			FETT G 5 A_CustomBulletAttack(6,0,1,random(3,6)*3,"EnemyBulletPuff",0,CBAF_NORANDOM)
			FETT G 0 A_FaceTarget
			FETT G 0 A_PlaySound("hans/attack")
			FETT G 5 A_CustomBulletAttack(6,0,1,random(3,6)*3,"EnemyBulletPuff",0,CBAF_NORANDOM)
			FETT E 7
			FETT E 0 A_CPosRefire
			Goto Missile+1
		Death:
			FETT A 0 A_GiveToTarget("Wolf_Score", 5000)
			FETT A 70 A_Scream
			FETT I 4 A_NoBlocking
			FETT JK 4
			FETT L -1 A_BossDeath
			Stop
	}
}